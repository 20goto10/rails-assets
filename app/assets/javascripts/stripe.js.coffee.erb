DEFAULT_AMOUNT = 5
SUCCESS_MESSAGE = 'Thank you for your donation! Your help keeps Rails Assets running.'

DonationButtonController = ($scope, $http, notifications, Popeye) ->
  handler = StripeCheckout.configure
    key: '<%= Rails.configuration.stripe[:publishable_key] %>'
    locale: 'auto'
    token: (token) ->
      onSuccess = (response) -> notifications.showSuccess(SUCCESS_MESSAGE)
      onError = (response) -> notifications.showError(message: response.data.error)

      $http.post(Routes.donations_path(), { amount: $scope.amount, token: token }).then onSuccess, onError

  $scope.amount = DEFAULT_AMOUNT

  $scope.invoke = ->
    modal = Popeye.openModal
      scope: $scope
      templateUrl: 'ng-templates/donate.html'
    modal.closed.then ->
      if $scope.invokeStripe
        $scope.invokeStripe = false
        handler.open
          name: 'Rails Assets'
          description: 'Donation'
          amount: $scope.amount * 100

  $scope.next = ->
    $scope.invokeStripe = true
    $scope.$close()


angular.module('rails-assets').controller 'DonationButtonController', [
  '$scope',
  '$http',
  'notifications',
  'Popeye',
  DonationButtonController
]
